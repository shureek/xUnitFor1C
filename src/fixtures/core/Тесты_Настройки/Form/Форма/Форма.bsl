&НаКлиенте
Перем КонтекстЯдра;
&НаКлиенте
Перем Ожидаем;
&НаКлиенте
Перем ПарсерКоманднойСтроки;
&НаКлиенте
Перем ПлагинНастроек;

//{ основная процедура для юнит-тестирования xUnitFor1C

&НаКлиенте
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	ПарсерКоманднойСтроки = КонтекстЯдра.Плагин("ПарсерКоманднойСтроки");
	ПлагинНастроек = КонтекстЯдра.Плагин("Настройки");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	НаборТестов.Добавить("ТестДолжен_ПрочитатьНастройкиПереданныеВКоманднойСтроке");
	НаборТестов.Добавить("ТестДолжен_ПрочитатьНастройкуПереданнуюВКоманднойСтроке");
КонецПроцедуры
//}

//{ блок юнит-тестов - сами тесты

&НаКлиенте
Процедура ПередЗапускомТеста() Экспорт
	
	ПропуститьТест = СтрНайти(ПараметрЗапуска, ПарсерКоманднойСтроки.Объект.ВозможныеКлючи.xddRun) = 0
		И ПлагинНастроек.ПолучитьНастройки() = Неопределено;
		
	Если ПропуститьТест Тогда
		КонтекстЯдра.ПропуститьТест("Пропускаю, потому что не передан параметр-конфиг в командной строке");
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаТеста() Экспорт
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ПрочитатьНастройкиПереданныеВКоманднойСтроке() Экспорт
	
	Настройки = ПлагинНастроек.ПолучитьНастройки();
	Ожидаем.Что(Настройки, "Не получили нужный тип настройки").ИмеетТип(Тип("ФиксированнаяСтруктура"));
	
	ПутьНастройки = "Тесты_Настройки";
	Настройка = Настройки[ПутьНастройки];
	
	ПроверитьНастройку(Настройка);
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ПрочитатьНастройкуПереданнуюВКоманднойСтроке() Экспорт
	
	ПутьНастройки = "Тесты_Настройки";
	Настройка = ПлагинНастроек.ПолучитьНастройку(ПутьНастройки);
	
	ПроверитьНастройку(Настройка);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНастройку(Знач Настройка)
	//Ожидаем.Что(Настройка, "Не получили нужное значение настройки").Равно("значение для Тесты_Настройки");
	Ожидаем.Что(Настройка, "Не получили нужный тип настройки").ИмеетТип(Тип("Структура"));
	Ожидаем.Что(Настройка.Свойство("Ключ"), "Не найден нужный ключ у настройки").ЭтоИстина();
	Ожидаем.Что(Настройка.Ключ, "Не получили нужное значение настройки").Равно("значение для Тесты_Настройки");
КонецПроцедуры

//} конец блока юнит-тестов
